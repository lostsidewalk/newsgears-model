
plugins {
    id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'signing'

group = 'com.lostsidewalk.newsgears'
version = '0.5.4'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // SLF4J
    implementation 'org.slf4j:slf4j-api:2.0.5'

    // validation-api
    api 'javax.validation:validation-api:2.0.1.Final'

    // jackson-annotation
    api 'com.fasterxml.jackson.core:jackson-annotations:2.15.1'
    // jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.1'
    // commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    // commons-collections4
    implementation 'org.apache.commons:commons-collections4:4.4'
    // jaxb-api
    api 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    // rome
    api 'com.rometools:rome-opml:2.1.0'
    // rome-modules
    api 'com.rometools:rome-modules:2.1.0'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

    annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.15.0'
    implementation 'com.github.therapi:therapi-runtime-javadoc:0.15.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'newsgears-model'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'newsgears-model'
                description = 'This module contains the basic Java data structures used throughout the FeedGears RSS platform.'
                url = 'https://www.lostsidewalk.com/dev/newsgears-model'
                licenses {
                    license {
                        name = 'GNU General Public License'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'elderscrollsjesus'
                        name = 'Michael Harris'
                        email = 'meh@lostsidewalk.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/lostsidewalk/newsgears-model.git'
                    developerConnection = 'scm:git:https://github.com/lostsidewalk/newsgears-model.git'
                    url = 'https://www.lostsidewalk.com/dev/newsgears-model'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
